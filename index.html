<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
    <title>Elm Type Definitions</title>
    <link rel="stylesheet" href="./assets/codemirror/main.css" />
    <link rel="stylesheet" href="./assets/style.css" />
  </head>
  <body>
    <script id="elm-d-ts" type="x-noparse-text/typescript">
      declare namespace Elm {
        type IncomingPorts<T> = {
          [P in keyof T]: {
            send(message: T[P]): void;
          };
        };

        type OutgoingPorts<T> = {
          [P in keyof T]: {
            subscribe(handler: (message: T[P]) => void): void;
            unsubscribe(handler: (message: T[P]) => void): void;
          };
        };

        type Flags<T extends Init> = T extends Init<infer F> ? F : never;

        type Ports<T extends Init> = T extends Init<any, infer In, infer Out>
          ? IncomingPorts<In> & OutgoingPorts<Out>
          : never;

        type Init<Flags = void, In = {}, Out = {}> = (Flags extends void
          ? {}
          : { flags: Flags }) &
          (keyof (IncomingPorts<In> & OutgoingPorts<Out>) extends never
            ? {}
            : {
                ports: (keyof In extends never ? {} : { incoming: In }) &
                  (keyof Out extends never ? {} : { outgoing: Out });
              });

        type App<Ports> = keyof Ports extends never ? {} : { ports: Ports };

        type Module = {
          init<T extends Init>(
            options: { node: HTMLElement } & (Flags<T> extends void
              ? {}
              : { flags: Flags<T> })
          ): App<Ports<T>>;
        };
      }

      declare var Elm: {
        [module: string]: Elm.Module | undefined;
      };
    </script>
    <script id="main-ts" type="x-noparse-text/typescript">
      (() => {
        type ElmInit = {
          flags: {
            title: string;
            sourceFiles: { name: string; content: string }[];
          };
          ports: {
            incoming: {
              localStorageGetResp: { value: string };
            };
            outgoing: {
              localStorageGetReq: { key: string };
              localStorageSet: { key: string; value: string };
              localStorageClear: null;
            };
          };
        };

        let elmDiv = document.createElement("div");
        document.body.appendChild(elmDiv);

        let sourceFiles = ["elm.d.ts", "main.ts"].map(name => {
          let elementId = name.replace(/\./g, "-");
          let content = readEmbeddedSourceCode(elementId);
          return { name, content };
        });

        let app = Elm.Main!.init<ElmInit>({
          node: elmDiv,
          flags: { title: "Elm Type Definitions", sourceFiles }
        });

        app.ports.localStorageGetReq.subscribe(({ key }) => {
          app.ports.localStorageGetResp.send({
            value: localStorage.getItem(key) || ""
          });
        });

        app.ports.localStorageSet.subscribe(({ key, value }) => {
          localStorage.setItem(key, value);
        });

        app.ports.localStorageClear.subscribe(() => {
          localStorage.clear();
        });

        function readEmbeddedSourceCode(elementId: string) {
          let scriptElement = document.getElementById(elementId);
          if (!scriptElement) {
            throw new Error(`Could not find script tag #${elementId}.`);
          }
          let lines = scriptElement.textContent!.split("\n").slice(1);
          let indent = lines[0].search(/[^\s]/);
          return lines.map(l => l.slice(indent)).join("\n");
        }
      })();
    </script>
    <script id="main-elm" type="x-noparse-text/elm">
      port module Main exposing (main)

      import Browser
      import Html as H exposing (Html)
      import Html.Attributes as A
      import Html.Events as E
      import Json.Decode as JD


      port localStorageGetReq : { key : String } -> Cmd msg


      port localStorageGetResp : ({ value : String } -> msg) -> Sub msg


      port localStorageSet : { key : String, value : String } -> Cmd msg


      port localStorageClear : () -> Cmd msg


      type alias SourceFile =
          { name : String
          , content : String
          }


      type alias Flags =
          { title : String
          , sourceFiles : List SourceFile
          }


      type alias Model =
          { title : String
          , sourceFiles : List SourceFile
          , waitingForJs : Bool
          , localStorageFormKey : String
          , localStorageFormValue : String
          }


      type Msg
          = UpdLocalStorageFormKey String
          | UpdLocalStorageFormValue String
          | GetFromLocalStorageReq
          | GetFromLocalStorageResp String
          | SetToLocalStorage
          | ClearLocalStorage
          | UpdSourceCode Int String


      main : Program Flags Model Msg
      main =
          Browser.element
              { init = init
              , view = view
              , update = update
              , subscriptions = subscriptions
              }


      init : Flags -> ( Model, Cmd Msg )
      init flags =
          let
              initialState =
                  { title = flags.title
                  , sourceFiles = flags.sourceFiles
                  , waitingForJs = False
                  , localStorageFormKey = ""
                  , localStorageFormValue = ""
                  }
          in
          ( initialState, Cmd.none )


      view : Model -> Html Msg
      view model =
          H.div [] <|
              [ H.fieldset [ A.disabled model.waitingForJs ]
                  [ H.h1 [] [ H.text model.title ]
                  , viewLabeledInput "Key" model.localStorageFormKey UpdLocalStorageFormKey
                  , viewLabeledInput "Value" model.localStorageFormValue UpdLocalStorageFormValue
                  , H.p [ A.class "row" ]
                      [ H.div [ A.class "spacer" ] []
                      , H.div [ A.class "buttons" ]
                          [ H.button [ E.onClick GetFromLocalStorageReq ] [ H.text "Get" ]
                          , H.button [ E.onClick SetToLocalStorage ] [ H.text "Set" ]
                          , H.button [ E.onClick ClearLocalStorage ] [ H.text "Clear" ]
                          , H.div [ A.class "foobar" ] []
                          ]
                      ]
                  ]
              ]
                  ++ viewSourceFiles model.sourceFiles


      viewSourceFiles : List SourceFile -> List (Html Msg)
      viewSourceFiles =
          List.indexedMap <|
              \index sourceFile ->
                  H.div [ A.class "codeViewerContainer" ]
                      [ H.h3 [] [ H.text sourceFile.name ]
                      , H.node "code-viewer"
                          [ A.attribute "editor-value" sourceFile.content
                          , E.on "editorChanged" <| JD.map (UpdSourceCode index) <| JD.at [ "detail", "value" ] <| JD.string
                          ]
                          []
                      ]


      viewLabeledInput : String -> String -> (String -> msg) -> Html msg
      viewLabeledInput label value msg =
          H.p [ A.class "row" ]
              [ H.label []
                  [ H.span [ A.class "label" ] [ H.text (label ++ ":") ]
                  , H.input [ A.value value, E.onInput msg ] []
                  ]
              ]


      update : Msg -> Model -> ( Model, Cmd Msg )
      update msg model =
          let
              currentKey =
                  model.localStorageFormKey

              currentValue =
                  model.localStorageFormValue
          in
          case msg of
              UpdLocalStorageFormKey newKey ->
                  ( { model | localStorageFormKey = newKey }, Cmd.none )

              UpdLocalStorageFormValue newValue ->
                  ( { model | localStorageFormValue = newValue }, Cmd.none )

              GetFromLocalStorageReq ->
                  ( { model | waitingForJs = True }, localStorageGetReq { key = currentKey } )

              GetFromLocalStorageResp newValue ->
                  ( { model | waitingForJs = False, localStorageFormValue = newValue }, Cmd.none )

              SetToLocalStorage ->
                  ( model, localStorageSet { key = currentKey, value = currentValue } )

              ClearLocalStorage ->
                  ( { model | localStorageFormKey = "", localStorageFormValue = "" }, localStorageClear () )

              UpdSourceCode updateIndex newContent ->
                  let
                      updateByIndex =
                          \index sourceFile ->
                              if index == updateIndex then
                                  { sourceFile | content = newContent }

                              else
                                  sourceFile

                      updatedSourceFiles =
                          List.indexedMap updateByIndex model.sourceFiles
                  in
                  ( { model | sourceFiles = updatedSourceFiles }, Cmd.none )


      subscriptions : Model -> Sub Msg
      subscriptions =
          always <| localStorageGetResp (\{ value } -> GetFromLocalStorageResp value)
    </script>
    <script src="./assets/codemirror/main.js"></script>
    <script src="./assets/codemirror/javascript.js"></script>
    <script src="./assets/codemirror/elm.js"></script>
    <script src="./dist/elm.js"></script>
    <script src="./dist/CodeViewer.js"></script>
    <script src="./dist/main.js"></script>
  </body>
</html>
